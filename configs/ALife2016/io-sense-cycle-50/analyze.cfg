
##########################################################################################################################
# Analysis for io-sense experiments
##########################################################################################################################

#####################################
# Some variables:
# e: experiment name
SET e io-sense-cycle-50
# r: replicate count
SET r 50
# u: update for most evolved population
SET u 100000
# d: base data directory
SET d /mnt/home/lalejini/Data/io-sense/$e
#####################################

# BATCH 0: Nand+Not- Environment tests
# BATCH 1: Nand-Not+ Environment tests

FORRANGE i 1 $r
  # For each trial:
  # Do some cleaning
  PURGE_BATCH 0
  PURGE_BATCH 1
  # Set current batch to 0
  SET_BATCH 0
  # Load most evolved population
  LOAD $d/data/single_runs_$i/data/detail-$u.spop
  # Get the dominant organism (max num_cpus)
  FIND_GENOTYPE num_cpus
  # Copy dominant organism into batch 1 (we'll need it in both batches)
  DUPLICATE 0 1

  ###### Nand+Not- tests ######
  # Set environment to nand+not-
  SET_BATCH 0
  SET v nand+not-
  SetReactionValue NOT -1.0
  SetReactionValue NAND 1.0
  # Run tests with dominant genome
  RECALC
  # Save out details
  PRINT  analysis/single_runs_$i dom_$v.gen
  TRACE  analysis/single_runs_$i/dom_$v_trace/
  DETAIL analysis/single_runs_$i/dom_$v_detail.html id num_cpus total_cpus length sequence task_list task_order viable merit fitness
  ###############################

  ###### Nand-Not+ tests ######
  # Set environment to nand-not+
  SET_BATCH 1
  SET v nand-not+
  SetReactionValue NOT 1.0
  SetReactionValue NAND -1.0
  # Run tests with dominant genome
  RECALC
  # Save out details
  PRINT analysis/single_runs_$i dom_$v.gen
  TRACE analysis/single_runs_$i/dom_$v_trace/
  DETAIL analysis/single_runs_$i/dom_$v_detail.html id num_cpus total_cpus length sequence task_list task_order viable merit fitness
  ###############################

END
