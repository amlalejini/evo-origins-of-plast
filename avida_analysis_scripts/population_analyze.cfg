
##########################################################################################################################
# Analysis for io-sense experiments
##########################################################################################################################

#####################################
# Variables in use: e, r, u, d, v, s
# Some variables:
# e: experiment name
SET e
# r: replicate count
SET r 50
# u: update for most evolved population
SET u 100000
SET s 10
# d: base data directory
SET d /mnt/scratch/lalejini/exp_ws/stepping_stones_of_plasticity/$e
#####################################

# BATCH 0: Nand+Not- Environment tests
# BATCH 1: Nand-Not+ Environment tests

FORRANGE i 0 $r
  # For each rep:
  FORRANGE k 1 $u $s
  # For each population through time:
    # Do some cleaning
    PURGE_BATCH 0
    PURGE_BATCH 1
    # Set current batch to 0
    SET_BATCH 0
    # Load most evolved population
    LOAD $d/data/single_runs_$i/data/detail-$k.spop
    FILTER num_cpus > 0
    # Copy the population into second batch (we'll be testing the population in two different environments)
    DUPLICATE 0 1

    ###### Nand+Not- tests ######
    # Set environment to nand+not-
    SET_BATCH 0
    SET v nand+not-
    SetReactionValue NOT -1.0
    SetReactionValue NAND 1.0
    # Run tests with pop
    RECALC
    # Save out details
    #PRINT  analysis/single_runs_$i/pop_$k/$v
    DETAIL analysis/single_runs_$i/pop_$k/$v/pop_detail.html id num_cpus total_cpus viable merit fitness update_born depth parent_dist length exe_length gest_time task.0 task.1 task.0:binary task.1:binary html.sequence
    DETAIL analysis/single_runs_$i/pop_$k/$v/pop_detail.dat id num_cpus viable fitness update_born depth parent_dist length task.0 task.1
    ###############################

    ###### Nand+Not- tests ######
    # Set environment to nand-not+
    SET_BATCH 1
    SET v nand-not+
    SetReactionValue NOT 1.0
    SetReactionValue NAND -1.0
    # Run tests with pop
    RECALC
    # Save out details
    #PRINT  analysis/single_runs_$i/pop_$k/$v
    DETAIL analysis/single_runs_$i/pop_$k/$v/pop_detail.html id num_cpus total_cpus viable merit fitness update_born depth parent_dist length exe_length gest_time task.0 task.1 task.0:binary task.1:binary html.sequence
    DETAIL analysis/single_runs_$i/pop_$k/$v/pop_detail.dat id num_cpus viable fitness update_born depth parent_dist length task.0 task.1
    ###############################
  END
END
