
##########################################################################################################################
# Analysis for io-sense experiments
##########################################################################################################################

#####################################
# Some variables:
# e: experiment name
SET e
# r: replicate count
SET r 50
# u: update for most evolved population
SET u 100000
# d: base data directory
SET d /mnt/scratch/lalejini/exp_ws/stepping_stones_of_plasticity/$e
#####################################

# BATCH 0: Nand+Not- Environment tests
# BATCH 1: Nand-Not+ Environment tests

FORRANGE i 1 $r
  # For each trial:
  # Do some cleaning
  PURGE_BATCH 0
  PURGE_BATCH 1
  # Set current batch to 0
  SET_BATCH 0
  # Load most evolved population
  LOAD $d/data/single_runs_$i/data/detail-$u.spop
  # Get the dominant organism's (max num_cpus) lineage
  FIND_LINEAGE num_cpus
  # Copy dominant organism's lineage into batch 1 (we'll need it in both batches)
  DUPLICATE 0 1

  ###### Nand+Not- tests ######
  # Set environment to nand+not-
  SET_BATCH 0
  SET v nand+not-
  SetReactionValue NOT -1.0
  SetReactionValue NAND 1.0
  # Run tests with dominant genome
  RECALC
  # Save out details
  DETAIL analysis/single_runs_$i/final_dominant/$v/lineage.html update_born depth parent_dist length fitness task.0 task.1 html.sequence
  DETAIL analysis/single_runs_$i/final_dominant/$v/lineage_tasks.dat update_born depth parent_dist fitness task.0 task.1
  ###############################

  ###### Nand-Not+ tests ######
  # Set environment to nand-not+
  SET_BATCH 1
  SET v nand-not+
  SetReactionValue NOT 1.0
  SetReactionValue NAND -1.0
  # Run tests with dominant genome
  RECALC
  # Save out details
  DETAIL analysis/single_runs_$i/final_dominant/$v/lineage.html update_born depth parent_dist length fitness task.0 task.1 html.sequence
  DETAIL analysis/single_runs_$i/final_dominant/$v/lineage_tasks.dat update_born depth parent_dist fitness task.0 task.1
  ###############################

END
